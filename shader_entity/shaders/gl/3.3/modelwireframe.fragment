#version 330 core

out vec4 fragmentColor;

in vec3 viewDirection;
in vec3 normal;
in vec3 gnormal;
in vec3 worldPosition;
in vec3 worldWater;
in vec3 barycentric;
in vec3 gcolor;

uniform vec4 ambient;
uniform vec4 diffuse;
uniform vec4 specular;
const float specularPower = 4.5;
const vec3 lightDirection = vec3(1.0, 0.0, 1.0);

uniform int lightingEnable;
uniform float transparency;

uniform vec3 minSpace;
uniform vec3 maxSpace;
uniform float bottom; 
uniform float topVisibleHeight;
uniform float bottomVisibleHeight;
uniform float error;
uniform float supportCos;

uniform int hoverState;
uniform int fanzhuan;
uniform int checkscope;

uniform float state;
uniform float nozzle;
uniform vec4 stateColors[6];
uniform vec4 customColor;

/* 剖面图参数 */
uniform vec3 sectionNormal;
uniform vec3 sectionFrontPos;
uniform vec3 sectionBackPos;

const vec3 wireframeColor = vec3(0.1);
const vec3 clearColor = vec3(0.27);
uniform int renderModel;
uniform int useVertexColor = 0;

bool frontFacing()
{
    vec3 fdx = dFdx(worldPosition);
    vec3 fdy = dFdy(worldPosition);
    return dot(normalize(gnormal), normalize(cross(fdx, fdy))) > -0.05;
}

vec4 directLight(vec3 light_dir, vec3 fnormal, vec4 core_color, vec4 ambient_color, vec4 diffuse_color, vec4 specular_color)
{
	float NdotL 		  = max(dot(fnormal, light_dir), 0.0);
	ambient_color 	  	  = ambient_color * core_color;

	vec3 freflection      = reflect(-light_dir, fnormal);
	vec3 fViewDirection   = normalize(viewDirection);
	float RdotV           = max(0.0, dot(freflection, fViewDirection)); 
	
	diffuse_color		  = NdotL * diffuse_color * core_color;
	specular_color        = specular_color * pow( RdotV, specularPower) * core_color;
	
	if (lightingEnable > 0)
		return ambient_color + diffuse_color + specular_color;
	else
		return core_color;
}

void main( void )
{
	/* 剖面 */
	float distanceToFrontPlane = dot(sectionNormal, worldPosition - sectionFrontPos);
	float distanceToBackPlane = dot(sectionNormal, worldPosition - sectionBackPos);
	if (distanceToFrontPlane < 0.0 || distanceToBackPlane > 0.0) 
		discard;

	if(checkscope > 0 && (worldPosition.z < bottomVisibleHeight || worldPosition.z > topVisibleHeight))
		discard;

	int stateInt = int(state);
	vec4 color;
	
	if (stateInt < 5)
	{
		color = stateColors[stateInt];
		if (useVertexColor == 1 && stateInt == 0) {
			color = vec4(gcolor, 1.0);
		}			
	} else {
		color = customColor;
	}

	vec3 fgnormal =	normalize(gnormal);
	
	if(checkscope > 0)
	{
		if(worldPosition.x < minSpace.x || worldPosition.y < minSpace.y || worldPosition.z < minSpace.z 
		|| worldPosition.x > maxSpace.x || worldPosition.y > maxSpace.y || worldPosition.z > maxSpace.z
		|| abs(worldPosition.z - bottom) < 0.05 )
		{
			// color = vec4(0.649, 0.516, 0.504, 1.0);
			color = stateColors[4];
		}
	}
	
	if(hoverState > 0)
	{
		float v = dot(fgnormal, vec3(0.0, 0.0, -1.0));
		if(v >= supportCos)
		{
			color.r += 0.8 * v;
		}
	}
	
	if(!frontFacing())
	{
		color = stateColors[5]; 
	}

	vec4 coreColor = vec4(color.rgb + vec3(0.1, -0.1, 0.0) * nozzle, 1.0);

	vec3 fnormal 		  =	normalize(normal);
	vec4 ambient_color 	  = ambient;
	vec4 diffuse_color    = diffuse;
	vec4 specular_color   = specular;
	
	vec3 lightDir = normalize(lightDirection);
	float normalCos = dot(fnormal, lightDir);
	if (normalCos >= 0.0)
		coreColor = directLight(lightDir, fnormal, coreColor, ambient_color, diffuse_color, specular_color);
	else
		coreColor = directLight(-lightDir, fnormal, coreColor, ambient_color, diffuse_color, specular_color);

	if (renderModel != 1) {
		vec3 baryDelta = fwidth(barycentric);
			
		vec3 edgeFactor = smoothstep(vec3(0.0), baryDelta * 1.0, barycentric);
		float minEdgeFactor = min(min(edgeFactor.x, edgeFactor.y), edgeFactor.z);

		if (renderModel == 2) {
			
			if (minEdgeFactor >= 1.0) {
                discard;
            }
            coreColor.rgb = mix(wireframeColor, clearColor, minEdgeFactor);

		} else if (renderModel == 3) {
			coreColor.rgb = mix(wireframeColor, coreColor.rgb, minEdgeFactor);
		}
	}

	fragmentColor = vec4(coreColor.rgb, transparency);
}
