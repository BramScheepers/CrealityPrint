#version 150 core
out vec4 fragmentColor;

in vec3 viewDirection;
in vec3 normal;
in vec3 gnormal;
in vec3 worldPosition;
in vec3 worldWater;
in vec3 barycentric;
in float colorFlag;

uniform vec4 ambient = vec4(0.3, 0.3, 0.3, 1.0);
uniform vec4 diffuse = vec4(0.8, 0.8, 0.8, 1.0);
uniform vec4 specular = vec4(0.3, 0.3, 0.3, 1.0);
const float specularPower = 4.5;
const vec3 lightDirection = vec3(1.0, 0.0, 1.0);
uniform vec4 palette[16];
const vec4 highlightOverhangsColor = vec4(0.50, 0.26, 0.26, 1.0);
uniform float highlightOverhangsRate;

/* 剖面图参数 */
uniform vec3 sectionNormal;
uniform vec3 sectionFrontPos;
uniform vec3 sectionBackPos;

const vec3 wireframeColor = vec3(0.1);
const vec3 clearColor = vec3(0.27);
uniform int renderModel;

bool frontFacing()
{
        vec3 fdx = dFdx(worldPosition);
        vec3 fdy = dFdy(worldPosition);
        return dot(gnormal, cross(fdx, fdy)) > 0.0;
} 

vec4 directLight(vec3 light_dir, vec3 fnormal, vec4 core_color, vec4 ambient_color, vec4 diffuse_color, vec4 specular_color)
{
	float NdotL 		  = max(dot(fnormal, light_dir), 0.0);
	ambient_color 	  	  = ambient_color * core_color;

	vec3 freflection      = reflect(-light_dir, fnormal);
	vec3 fViewDirection   = normalize(viewDirection);
	float RdotV           = max(0.0, dot(freflection, fViewDirection)); 
	
	diffuse_color		  = NdotL * diffuse_color * core_color;
	specular_color        = specular_color * pow( RdotV, specularPower) * core_color;
	
	return ambient_color + diffuse_color + specular_color;
}

void main( void )
{
	/* 剖面 */
	float distanceToFrontPlane = dot(sectionNormal, worldPosition - sectionFrontPos);
	float distanceToBackPlane = dot(sectionNormal, worldPosition - sectionBackPos);
	if (distanceToFrontPlane < 0.0 || distanceToBackPlane > 0.0) 
		discard;

	vec4 color;
	if (dot(gnormal, vec3(0.0, 0.0, -1.0)) > (1.0 - highlightOverhangsRate))
	{
		color = highlightOverhangsColor;
	}
	else 
	{
		color = palette[int(round(colorFlag))];
	}

	vec4 coreColor = color;
	vec3 fgnormal =	normalize(gnormal);

	vec3 fnormal 		  =	normalize(normal);
	vec4 ambient_color 	  = ambient;
	vec4 diffuse_color    = diffuse;
	vec4 specular_color   = specular;
	
	vec3 lightDir = normalize(lightDirection);
	float normalCos = dot(fnormal, lightDir);
	if (normalCos >= 0.0)
		coreColor = directLight(lightDir, fnormal, coreColor, ambient_color, diffuse_color, specular_color);
	else
		coreColor = directLight(-lightDir, fnormal, coreColor, ambient_color, diffuse_color, specular_color);

	if(!frontFacing())
	{
		color = vec4(0.2, 0.2, 0.2, 0.0);   
		vec3 fbnormal = -fnormal;
		vec4 ambient_color_t 	= ambient + vec4(0.2, 0.2, 0.2, 0.0);
		vec4 diffuse_color_t    = diffuse + vec4(0.08, 0.08, 0.08, 0.0);
		vec4 specular_color_t   = specular;
		coreColor = directLight(lightDir, fbnormal, color, ambient_color_t, diffuse_color_t, specular_color_t);
	}

	if (renderModel != 1) {
		vec3 baryDelta = fwidth(barycentric);
			
		vec3 edgeFactor = smoothstep(vec3(0.0), baryDelta * 1.0, barycentric);
		float minEdgeFactor = min(min(edgeFactor.x, edgeFactor.y), edgeFactor.z);

		if (renderModel == 2) {
			
			if (minEdgeFactor >= 1.0) {
                discard;
            }
            coreColor.rgb = mix(wireframeColor, clearColor, minEdgeFactor);

		} else if (renderModel == 3) {
			coreColor.rgb = mix(wireframeColor, coreColor.rgb, minEdgeFactor);
		}
	}

	fragmentColor = vec4(coreColor.rgb, 1.0);
}
